apply plugin: 'application'

mainClassName = "com.comsysto.buildlight.application.Runner"

group = 'com.comsysto.buildlight'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/java'
            srcDir 'src/main/resources'
        }
    }
    test {
        resources {
            srcDir 'src/test/java'
            srcDir 'src/test/resources'
        }
    }
}

configurations {
    all*.exclude group: "com.codeminders"
    all*.exclude group: "org.rxtx"
}

dependencies {
    compile project(":app:buildlight")
    compile files("lib/hidapi-pi-1.1.jar")
    compile files("lib/RXTXcomm-2.2pre2.jar")
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Build Light Application for Raspberry PI",
                "Implementation-Version": version,
                "Author": "Bernd Zuther <bernd.zuther@comsysto.com>",
                "Main-Class": mainClassName
        )
    }
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dfile.encoding=utf-8 -Djava.library.path=/usr/lib/jni/"')
        windowsScript.text = windowsScript.text.replace('DEFAULT_JVM_OPTS=', 'DEFAULT_JVM_OPTS="-Dfile.encoding=utf-8 -Djava.library.path=/usr/lib/jni/"')
    }
}