apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

test.onlyIf { !Boolean.getBoolean('skip.tests') }

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked -Xlint:deprecation"
}

group = 'com.github.zutherb.buildlight.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

//load version from the root folder
def props = new Properties();
file("gradle.properties").withInputStream { props.load(it) }
props.each({ project.ext[it.key] = it.value })

ext {
    slf4jVersion = "1.7.5"
    commonsLangVersion = "2.6"
    logbackVersion = "1.0.13"
    groovyVersion = "2.1.6"
    junitVersion = "4.11"
    mockitoVersion = "1.9.5"
    jettyVersion = "8.1.12.v20130726"
    springVersion = "3.2.9.RELEASE"
    jacksonVersion = "1.9.13"
    httpClientVersion = "4.2.5"
    javaHome = System.properties['java.home']
}

configurations {
    all*.exclude module: "slf4j-log4j12"
    all*.exclude module: "jcl-over-slf4"
    all*.exclude module: "commons-logging"
    all*.exclude module: "log4j"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        maven{
            url 'http://maven.springsource.org/release'
        }
    }

    ext {
        commonDeps = [
                "org.slf4j:slf4j-api:$slf4jVersion",
                "ch.qos.logback:logback-classic:$logbackVersion",
                "org.codehaus.groovy:groovy-all:$groovyVersion",
                "commons-lang:commons-lang:$commonsLangVersion",
                "commons-collections:commons-collections:3.2.1",
                "commons-io:commons-io:2.4",
                "org.apache.commons:commons-math:2.2",
                "org.apache.commons:commons-exec:1.1"
        ]

        javaFxDeps = [
            files("$javaHome/lib/jfxrt.jar"),
            files("$javaHome/./lib/ant-javafx.jar")
        ]

        springDeps = [
                "org.springframework:spring-core:$springVersion",
                "org.springframework:spring-beans:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-context-support:$springVersion"
        ]

        springMvcDeps = [
                "org.springframework:spring-web:$springVersion",
                "org.springframework:spring-webmvc:$springVersion"
        ]

        jettyDeps = [
                "org.eclipse.jetty.aggregate:jetty-all-server:$jettyVersion",
                "org.eclipse.jetty.orbit:javax.servlet:3.0.0.v201112011016@jar",
                "org.eclipse.jetty:jetty-continuation:$jettyVersion",
                "org.eclipse.jetty:jetty-http:$jettyVersion"
        ]

        testDeps = [
                "org.springframework:spring-test:$springVersion",
                "junit:junit:$junitVersion",
                "com.jayway.jsonpath:json-path:0.8.1",
                "org.mockito:mockito-all:$mockitoVersion"
        ]

        jacksonDeps = [
                "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion",
                "org.codehaus.jackson:jackson-core-asl:$jacksonVersion"
        ]
    }


}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}